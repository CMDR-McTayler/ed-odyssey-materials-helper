import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.beryx:badass-jlink-plugin:2.24.1"
        classpath "io.freefair.gradle:lombok-plugin:5.3.3.3"
    }
}
apply plugin: "io.freefair.lombok"
apply plugin: 'idea' // optional (to generate IntelliJ IDEA project files)
apply plugin: "java"
apply plugin: "application"
apply plugin: "org.beryx.jlink"
group 'nl.jixxed'

OperatingSystem os = DefaultNativePlatform.currentOperatingSystem;
java {
    modularity.inferModulePath.set(true)
}
jar {
    manifest {
        attributes 'Name': 'nl/jixxed/eliteodysseymaterials',
                'Implementation-Title': 'Elite Dangerous Odyssey Materials Helper',
                'Implementation-Version': project.version
    }
}
repositories {
    mavenCentral()
}
application {
    mainModule = "nl.jixxed.eliteodysseymaterials"
    mainClass = "nl.jixxed.eliteodysseymaterials.Main"
}

if (os.isWindows()) {
    ext { packageType = "msi" }
} else {
    ext { packageType = "deb" }
}
jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Elite Dangerous Odyssey Materials Helper'
        jvmArgs = [
                '-Dapp.version=' + project.version
        ]
    }
    jpackage {
        appVersion = project.version
        installerType = project.ext.packageType
        if (installerType == 'msi') {
            imageOptions += ['--icon', 'src/main/resources/images/application/rocket.ico']
            installerOptions = [
                    '--description', 'Installer for ED Odyssey Materials Helper',
                    '--copyright', 'Copyright 2021 CMDR Jixxed',
                    '--name', 'Elite Dangerous Odyssey Materials Helper',
                    '--app-version', project.version
            ]
        }
        if (installerType == 'deb') {
            imageOptions += ['--icon', 'src/main/resources/images/application/rocket.png']
            installerOptions += [
                    '--linux-menu-group', 'Other',
                    '--linux-shortcut'
            ]
        }
    }
}
def platform = 'linux'
if (os.isWindows()) {
    platform = 'win'
}
dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation "org.junit.platform:junit-platform-suite-api:1.7.2"
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.11.0'
    testImplementation "org.assertj:assertj-core:3.19.0"

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.3'
    implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.0.13'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'org.slf4j:slf4j-simple:1.7.32'

    implementation('org.jfxtras:jmetro:11.6.15') {
        exclude group: 'org.openjfx'
    }
    implementation("org.openjfx:javafx-base:16:${platform}")
    implementation("org.openjfx:javafx-controls:16:${platform}")
    implementation("org.openjfx:javafx-fxml:16:${platform}")
    implementation("org.openjfx:javafx-graphics:16:${platform}")
}
jpackage { dependsOn test }

test {
    useJUnitPlatform {

    }
}
mainClassName = 'nl.jixxed.eliteodysseymaterials.Main'